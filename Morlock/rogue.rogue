class RoguePackage : Package
  # Base class Package is defined here:
  # https://github.com/brombres/Morlock/blob/main/Source/Package.rogue
  PROPERTIES
    name = "brombres/rogue"

  METHODS
    method init
       scan_repo_releases

    method build
      forEach (folder in File(".").listing(&folders,&ignore_hidden))
        if (System.is_windows)
          if (File(folder/"make.bat").exists)
            execute "cd $ && make.bat build" (File(folder).esc)
            archive_folder = folder
            return
          endIf
        else
          if (File(folder/"Makefile").exists)
            execute "cd $ && make build" (File(folder).esc)
            archive_folder = folder
            return
          endIf
        endIf
      endForEach

      if (not is_unpacked)
        # Most likely the script just didn't call unpack() yet.
        unpack
        is_unpacked = true
        build
      else
        throw error( "Unable to determine how to build $ after unpacking it."(url) )
      endIf

    method install
      download
      unpack
      build
      install_executable( &dest_filename="roguec" )

      local libs = Files( archive_folder/"Source/Libraries", "**" )
      libs.sync_to( install_folder/"Libraries" )

    method handle_uninstall
      throw error( "Essential package $ cannot be uninstalled."(name) )
endClass


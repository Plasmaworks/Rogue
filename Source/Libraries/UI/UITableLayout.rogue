module UI
uses Utility/WorkList

class UITableLayout : UILayout
  PROPERTIES
    is_adding  : Logical
    next_empty : Int32
    columns    = UITableColumnInfo[]
    rows       = UITableRowInfo[]
    span       = XY[]

  METHODS
    method init
      prior.init
      resize( 1, 1 )

    method init( size:XY )
      prior.init( size )
      resize( 1, 1 )

    method init( column_count:Int32, row_count:Int32 )
      prior.init
      resize( column_count, row_count )

    method add( component:UIComponent ) [override]
      if (is_adding)
        prior.add( component )
      else
        is_adding = true

        if (next_empty == -1)
          if (columns.is_empty)
            resize( 1, rows.count )
            add( component )
            is_adding = false
            return
          elseIf (rows.count == 1)
            resize( columns.count+1, rows.count )
          else
            resize( columns.count, rows.count+1 )
          endIf
        endIf

        set( next_empty, component )
        _advance_next_empty

        is_adding = false
      endIf

    method add( component:UIComponent, span:XY )
      if (span == XY(1,1))
        add( component )  # regular add
        return

      elseIf (next_empty == -1)
        if (rows.count == 1 and span.y == 1)
          # Extend new row
          set( columns.count, 0, component, span )
        else
          # Make new row(s)
          set( 0, rows.count, component, span )
        endIf

      else
        set( next_empty%columns.count, next_empty/columns.count, component, span )

      endIf

    method add_row
      resize( columns.count, rows.count+1 )

    method column( i:Int32 )->UITableColumnInfo
      if (i < 0 or i >= columns.count) throw OutOfBoundsError( i, columns.count )
      return columns[i]

    method draw_subcomponents( draw_bounds:Box )
      use subcomponents = WorkList<<UIComponent>>
        (forEach in children).collect_drawable( subcomponents )

        local column_count = columns.count
        forEach (j of rows)
          forEach (i of columns)
            local index = j*column_count+i
            if (span[index].min > 0)
              subcomponents[index].draw( draw_bounds )
            endIf
          endForEach
        endForEach
      endUse

    method get( i:Int32, j:Int32 )->UIComponent
      local index = index_of( i, j )
      if (not index) return null
      return get( index.value )

    method index_of( i:Int32, j:Int32 )->Int32?
      if (i < 0 or i >= columns.count) return null
      if (j < 0 or j >= rows.count)    return null
      return j*columns.count + i

    method measure_content_height( subcomponents:UIComponent[] )->Real64?
      local total_h = 0.0
      forEach (j of rows)
        local h = _row_height( subcomponents, j )
        if (not h) return null
        total_h += h.value
      endForEach
      return total_h

    method measure_content_width( subcomponents:UIComponent[] )->Real64?
      local total_w = 0.0
      forEach (i of columns)
        local w = _column_width( subcomponents, i )
        if (not w) return null
        total_w += w.value
      endForEach
      return total_w

    method measure_height( container_height:Real64? )->Real64?
      if (is_fixed_height) return size.y

      if (not ((forEach in rows).fixed_height))
        return prior.measure_height( container_height )
      endIf
      local h = 0.0
      h += (forEach in rows).fixed_height.value
      return h

    method measure_width( container_width:Real64? )->Real64?
      if (is_fixed_width) return size.x

      if (not ((forEach in columns).fixed_width))
        return prior.measure_width( container_width )
      endIf

      local w = 0.0
      w += (forEach in columns).fixed_width.value
      return w

    method resize( new_columns:Int32, new_rows:Int32 )
      next_empty = -1
      if (count)
        # Remove cells or add empty cells to maintain structure
        if (new_columns == 0 or new_rows == 0)
          clear
        else
          use new_cells = WorkList<<UIComponent>>
            forEach (j in 0..<new_rows)
              forEach (i in 0..<new_columns)
                local cell = get( i, j )
                if (cell) new_cells.add( cell )
                else      new_cells.add( UITableEmptyCell() )
              endForEach
            endForEach
            clear
            prior.add( forEach in new_cells )
          endUse

          use new_span = WorkList<<XY>>
            forEach (j in 0..<new_rows)
              forEach (i in 0..<new_columns)
                local index = index_of( i, j )
                if (index?) new_span.add( span[index.value] )
                else        new_span.add( XY(1,1) )
              endForEach
            endForEach
            span.clear
            span.add( new_span )
          endUse
        endIf
      else
        loop (new_columns*new_rows) prior.add( UITableEmptyCell() )
        if (count) next_empty =  0
        else       next_empty = -1
      endIf

      while (columns.count < new_columns) columns.add( UITableColumnInfo() )
      while (rows.count < new_rows) rows.add( UITableRowInfo() )
      columns.discard_from( new_columns )
      rows.discard_from( new_rows )
      loop (columns.count*rows.count - span.count) span.add( XY(1,1) )

      _advance_next_empty

    method row( j:Int32 )->UITableRowInfo
      if (j < 0 or j >= rows.count) throw OutOfBoundsError( j, rows.count )
      return rows[j]

    method set( i:Int32, j:Int32, component:UIComponent )
      if (component is null) component = UITableEmptyCell()
      if (i >= columns.count or j >= rows.count)
        resize( columns.count.or_larger(i+1), rows.count.or_larger(j+1) )
      endIf

      local index = j * columns.count + i
      temporarily is_adding = true
        set( index, component )
      endTemporarily
      if (index == next_empty) _advance_next_empty

    method set( i:Int32, j:Int32, component:UIComponent, span:XY )
      if (span == XY(1,1))
        set( i, j, component )
        return
      else
        set( i+(span.x-1), j+(span.y-1), UITableEmptyCell() )
        set( i, j, component )
        set_span( i, j, span )
      endIf

    method set_span( i:Int32, j:Int32, new_span:XY )
      local index = index_of(i,j)
      require index.exists
      local old_span = span[index.value]
      if (old_span.max > 1)
        # Reset the old span shadow to contain spans of 1 before zeroing the new shadow
        _set_span_shadow( i, j, old_span.x, old_span.y, 1 )
      endIf
      local w = new_span.x.floor.clamped_high( columns.count - i )
      local h = new_span.y.floor.clamped_high( rows.count - j )
      _set_span_shadow( i, j, w, h, 0 )
      span[index.value] = XY(w,h)
      _advance_next_empty

    method span( i:Int32, j:Int32 )->XY
      local index = index_of(i,j)
      require index
      return span[index.value]

    method update_subcomponent_heights( subcomponents:UIComponent[], container_height:Real64 )
      local total_h = 0.0
      local undefined_count = 0
      local undefined_weight = 0.0

      use row_heights = WorkList<<Real64?>>
        forEach (j of rows)
          local h = _row_height( subcomponents, j )
          if (h)
            total_h += h.value
            row_heights.add( h )
          else
            ++undefined_count
            undefined_weight += rows[j].weight
            row_heights.add( null )
          endIf
        endForEach

        undefined_weight .= clamped_low(1)
        local excess_h = container_height - total_h
        local remaining_h = excess_h
        forEach (j of rows)
          local h = row_heights[j]
          if (not h)
            if (undefined_count == 1)
              h = remaining_h
            else
              h = (excess_h * (rows[j].weight / undefined_weight)).floor
              remaining_h -= h.value
            endIf
            --undefined_count
            row_heights[j] = h
          endIf

          forEach (row at row_j in rows)
            row.height = row_heights[row_j].value
          endForEach
        endForEach

        forEach (j of rows)
          local column_count = columns.count
          forEach (i of columns)
            local index = j*column_count + i
            if (span[index].y)
              local cell = subcomponents[ index ]
              cell.update_layout_height( _spanned_height(i,j) )
            endIf
          endForEach
        endForEach
      endUse

    method update_subcomponent_widths( subcomponents:UIComponent[], container_width:Real64 )
      local total_w = 0.0
      local undefined_count = 0
      local undefined_weight = 0.0

      use column_widths = WorkList<<Real64?>>
        forEach (i of columns)
          local w = _column_width( subcomponents, i )
          if (w)
            total_w += w.value
            column_widths.add( w )
          else
            ++undefined_count
            undefined_weight += columns[i].weight
            column_widths.add( null )
          endIf
        endForEach

        undefined_weight .= clamped_low(1)
        local excess_w = container_width - total_w
        local remaining_w = excess_w
        forEach (i of columns)
          local w = column_widths[i]
          if (not w)
            if (undefined_count == 1)
              w = remaining_w
            else
              w = (excess_w * (columns[i].weight / undefined_weight)).floor
              remaining_w -= w.value
            endIf
            --undefined_count
            column_widths[i] = w
          endIf

          forEach (column at column_i in columns)
            column.width = column_widths[column_i].value
          endForEach
        endForEach

        forEach (i of columns)
          local column_count = columns.count
          forEach (j of rows)
            local index = j*column_count + i
            if (span[index].x)
              local cell = subcomponents[ index ]
              cell.update_layout_width( _spanned_width(i,j) )
            endIf
          endForEach
        endForEach
      endUse

    method update_subcomponent_placement( subcomponents:UIComponent[], container:Box ) [override]
      local column_count = columns.count
      local cursor_start = container.position
      forEach (row at j in rows)
        local cursor = cursor_start
        forEach (column at i in columns)
          local index = j*column_count + i
          if (span[index].x)
            local cell = subcomponents[ index ]
            cell.update_layout_placement( Box(cursor,cell.size) )
          endIf
          cursor += XY(column.width,0)
        endForEach
        cursor_start += XY(0,row.height)
      endForEach

    method _advance_next_empty
      forEach (i in next_empty..<count)
        local cell = this[i]
        if (cell instanceOf UITableEmptyCell and span[i].x)
          next_empty = i
          return
        endIf
      endForEach
      next_empty = -1

    method _column_width( subcomponents:UIComponent[], i:Int32 )->Real64?
      local max_w = columns[i].fixed_width
      if (max_w) return max_w

      local column_count = columns.count
      forEach (j of rows)
        local index = j*column_count + i
        if (span[index].x == 1)
          local cell = subcomponents[ index ]
          local w = cell.measure_width( null )
          if (w)
            if (max_w) max_w = max_w.value.or_larger( w.value )
            else       max_w = w.value
          endIf
        endIf
      endForEach

      return max_w

    method _row_height( subcomponents:UIComponent[], j:Int32 )->Real64?
      local max_h = rows[j].fixed_height
      if (max_h) return max_h

      local column_count = columns.count
      forEach (i of columns)
        local index = j*column_count + i
        if (span[index].y == 1)
          local cell = subcomponents[ index ]
          local h = cell.measure_height( null )
          if (h)
            if (max_h) max_h = max_h.value.or_larger( h.value )
            else       max_h = h.value
          endIf
        endIf
      endForEach

      return max_h

    method _set_span_shadow( x:Int32, y:Int32, w:Int32, h:Int32, value:Int32 )
      local cols = columns.count
      local new_span = XY(value,value)
      forEach (j in y..<y+h)
        forEach (i in x..<x+w)
          span[ j*cols+i ] = new_span
        endForEach
      endForEach

    method _spanned_height( i:Int32, j:Int32 )->Real64
      local spanned_rows = span[ j*columns.count+i ].y
      local result = 0.0
      forEach (y in 0..<spanned_rows)
        result += rows[j+y].height
      endForEach
      return result

    method _spanned_width( i:Int32, j:Int32 )->Real64
      local spanned_columns = span[ j*columns.count+i ].x
      local result = 0.0
      forEach (x in 0..<spanned_columns)
        result += columns[i+x].width
      endForEach
      return result
endClass

class UITableEmptyCell : UIComponent;

class UITableColumnInfo
  PROPERTIES
    fixed_width : Real64?
    width       : Real64   # computed
    weight      = 1.0
endClass

class UITableRowInfo
  PROPERTIES
    fixed_height : Real64?
    height       : Real64
    weight       = 1.0
endClass


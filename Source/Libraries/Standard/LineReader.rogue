class LineReader : Reader<<String>>
  PROPERTIES
    source : Reader<<Character>>
    next   : String
    buffer = StringBuilder()
    prev   : Character

  METHODS
    method init( source )
      next = null

    method init( reader:Reader<<Byte>>, encoding=StringEncoding.AUTODETECT:StringEncoding )
      if (encoding == StringEncoding.EXTENDED_ASCII) init( ExtendedASCIIReader(reader) )
      else                                           init( UTF8Reader(reader) )

    method init( file:File, encoding=StringEncoding.AUTODETECT:StringEncoding )
      init( file.reader, encoding )

    method init( string:String )
      init( string.reader )

    method close
      source.close

    method has_another->Logical
      return prepare_next

    method peek->String
      prepare_next
      return next

    method prepare_next->Logical
      if (next) return true

      if (not source.has_another) return false

      prev = 0
      buffer.clear
      while (source.has_another)
        local ch = source.read
        if (ch == '\r') nextIteration
        if (ch == '\n')
          prev = '\n'
          escapeWhile
        endIf
        buffer.print( ch )
      endWhile

      next = buffer->String
      return true

    method read->String
      local result = peek
      next = null
      ++position
      return result

    method reset->this
      seek( 0 )
      return this

    method seek( n:Int32 )->this
      if (n < 0) n = 0

      if (n < position)
        source.reset
        position = 0
        next = null
      endIf

      while (n > position)
        read
      endWhile

      return this

    method to->String[]
      local lines = String[]
      forEach (line in this) lines.add( line )
      return lines

endClass


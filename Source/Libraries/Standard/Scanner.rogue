class Scanner : Reader<<Character>>
  PROPERTIES
    data           : Character[]
    source         : String
    position       : Int32
    count          : Int32
    line           : Int32
    column         : Int32
    spaces_per_tab : Int32

  METHODS
    method init( source, spaces_per_tab=0, &preserve_crlf )
      local tab_count = 0

      if (spaces_per_tab)
        tab_count = source.count('\t')
      endIf

      localize spaces_per_tab
      local new_count = source.count + tab_count*spaces_per_tab - tab_count
      data = Character[]( new_count )

      native @|char* src = $source->utf8 - 1;
              |RogueCharacter* dest = $data->data->as_characters - 1;
              |for (int i=$source->character_count; --i>=0; )
              |{
              |  RogueCharacter ch = (RogueCharacter) *(++src);
              |  if (ch == '\t')
              |  {
              |    if ($spaces_per_tab)
              |    {
              |      for (int j=$spaces_per_tab; --j>=0; ) *(++dest) = ' ';
              |    }
              |    else
              |    {
              |      *(++dest) = ch;
              |    }
              |  }
              |  else if (ch == '\r')
              |  {
              |    if ($preserve_crlf) *(++dest) = '\r';
              |    else                --$new_count;
              |  }
              |  else if (ch & 0x80)
              |  {
              |    if (ch & 0x20)
              |    {
              |      if (ch & 0x10)
              |      {
              |        ch  = ((ch&7)<<18) | ((*(++src) & 0x3F) << 12);
              |        ch |= (*(++src) & 0x3F) << 6;
              |        *(++dest) = ch | (*(++src) & 0x3F);
              |      }
              |      else
              |      {
              |        ch  = ((ch&15)<<12) | ((*(++src) & 0x3F) << 6);
              |        *(++dest) = ch | (*(++src) & 0x3F);
              |      }
              |    }
              |    else
              |    {
              |      *(++dest) = ((ch&31)<<6) | (*(++src) & 0x3F);
              |    }
              |  }
              |  else
              |  {
              |    *(++dest) = ch;
              |  }
              |}
      data.count = new_count
      count = new_count
      line = 1
      column = 1
      position = 0

      source = String( data )

    method init( file:File, spaces_per_tab=0 )
      init( file.load_as_string, spaces_per_tab )

    method init( source:Character[], spaces_per_tab=0, &preserve_crlf )
      init( String(source), &=spaces_per_tab, &=preserve_crlf )

    method consume( ch:Character )->Logical
      if (peek != ch) return false
      read
      return true

    method consume( text:String )->Logical
      forEach (ch at index in text)
        if (ch != peek(index)) return false
      endForEach

      # Found a match; discard the characters
      loop (text.count) read

      return true

    method consume_id( text:String )->Logical
      local ch = peek( text.count )
      if (ch.is_alphanumeric or ch == '_') return false  # not end of identifier
      return consume( text )

    method consume_eols->Logical
      local found = false
      while (consume('\n')) found = true
      return found

    method consume_spaces->Logical
      local found = false
      while (consume(' ')) found = true
      return found

    method consume_whitespace->Logical
      local found = false
      while (consume(' ') or consume('\t')) found = true
      return found

    method has_another->Logical
      return (position < count)

    method has_another( n:Int32 )->Logical
      return (position+n <= count)

    method next_is( ch:Character )->Logical
      if (position == count) return 0
      return (ch == data[position])

    method next_is_hex_digit->Logical
      local ch = peek
      return (ch >= '0' and ch <= '9') or (ch >= 'a' and ch <= 'f') or (ch >= 'A' and ch <= 'F')

    method next_is_identifier( &allow_dollar )->Logical
      return peek.is_identifier( &start, &=allow_dollar )

    method next_is_number( base=10:Int32 )->Logical
      if (next_is('-')) return peek(1).is_number( base )
      return peek.is_number( base )

    method next_is_real_number->Logical
      if (not next_is_number) return false
      use buffer = StringBuilder.pool
        local pos = this.position
        local line = this.line
        local col  = this.column
        local result = scan_number_string( buffer )
        this.position = pos
        this.line = line
        this.column = col
        return result
      endUse

    method next_is_string->Logical
      local ch = peek
      return (ch == '"' or ch == '\'')

    method peek->Character
      if (position == count) return 0->Character
      return data[position]

    method peek( lookahead:Int32 )->Character
      # Zero-based
      local peek_pos = position + lookahead
      if (peek_pos >= count) return 0->Character
      return data[peek_pos]

    method read->Character
      local result = data[ position ]->Character
      ++position
      if (result == 10)
        ++line
        column = 1
      else
        ++column
      endIf
      return result

    method read_hex_value->Int32
      local ch = read
      if (ch >= '0' and ch <= '9') return (ch - '0')
      if (ch >= 'a' and ch <= 'f') return 10 + (ch - 'a')
      if (ch >= 'A' and ch <= 'F') return 10 + (ch - 'A')
      return 0

    method read_line->String
      use buffer = StringBuilder.pool
        while (has_another)
          local ch = read
          if (ch == '\n') escapeWhile
          buffer.print( ch )
        endWhile
        return buffer->String
      endUse

    method reset->this
      count = data.count
      return seek( 0 )

    method scan_hex_sequence( buffer:StringBuilder )
      local first = true
      while (first or consume(','))
        first = false
        local ch = scan_hex_value( &variable_length )->Character
        buffer.print( ch )
      endWhile

    method scan_hex_value( digits=6:Int32, &variable_length )->Int32
      local value = 0
      local i = 1
      while (i <= digits)
        if (not has_another or not next_is_hex_digit) return value
        local intval = read.to_number(16)
        value = (value:<<:4) + intval
        ++i
      endWhile
      return value

    method scan_identifier( &allow_dollar )->String
      if (not peek.is_identifier(&start,&=allow_dollar)) return null
      use buffer = StringBuilder.pool
        buffer.print( read )
        while (peek.is_identifier(&=allow_dollar)) buffer.print( read )
        return buffer->String
      endUse

    method scan_int64( base=10:Int32 )->Int64
      local is_negative = consume( '-' )
      local result : Int64
      while (peek.is_number(base))
        result = result * base + read.to_number(base)
      endWhile
      if (is_negative) return -result
      else             return result

    method scan_number_string( buffer:StringBuilder )->Logical
      # Returns true if number string contains a decimal point or an exponent
      local is_real = false
      if (consume('-')) buffer.print( '-' )
      while (peek.is_number) buffer.print( read )
      if (peek == '.' and peek(1) != '.')
        is_real = true
        consume('.')
        buffer.print( '.' )
        while (peek.is_number) buffer.print( read )
      endIf
      if (consume('e') or consume('E'))
        is_real = true
        buffer.print( 'e' )
        if (consume('-'))   buffer.print( '-' )
        else consume( '+' ) buffer.print( '+' )
        while (peek.is_number) buffer.print( read )
      endIf
      return is_real

    method scan_real64->Real64
      use buffer = StringBuilder.pool
        scan_number_string( buffer )
        return buffer->Real64
      endUse

    method scan_string->String
      local ch = peek
      if (ch == '"')  return scan_string( '"', '"' )
      if (ch == '\'') return scan_string( '\'', '\'' )
      return null

    method scan_string( open:Character, close:Character )->String
      if (not consume(open)) return null
      use buffer = StringBuilder.pool
        while (has_another and not consume(close))
          scan_string_character( buffer )
        endWhile
        return buffer->String
      endUse

    method scan_string_character( buffer:StringBuilder )
      if (not has_another) return

      local ch = peek

      if (ch == '\\')
        read

        local value : Int32
        if     (consume('b')) value = 8
        elseIf (consume('e')) value = 27
        elseIf (consume('f')) value = 12
        elseIf (consume('n')) value = '\n'
        elseIf (consume('r')) value = '\r'
        elseIf (consume('t')) value = '\t'
        elseIf (consume('v')) value = 11
        elseIf (consume('0')) value = '\0'
        elseIf (consume('/')) value = '/'
        elseIf (consume('?')) value = '?'
        elseIf (consume('\''))value = '\''
        elseIf (consume('\\'))value = '\\'
        elseIf (consume('"')) value = '"'
        elseIf (consume('x')) value = scan_hex_value(2)
        elseIf (consume('u')) value = scan_hex_value(4)
        elseIf (consume('[')) scan_hex_sequence( buffer ); consume(']'); return
        elseIf (has_another)  value = read
        else                  value = '\\'

        buffer.print( value->Character )

      else
        local value = read->Int32
        if ((value & 0x80) != 0)
          # Handle UTF8 encoding
          local ch2 = read->Int32

          if ((value & 0x20) == 0)
            # %110xxxxx 10xxxxxx
            value = value & 0x1f
            ch2 = value & 0x3f
            buffer.print( ((value:<<:6) | ch2)->Character )
          else
            # %1110xxxx 10xxxxxx 10xxxxxx
            local ch3 = read->Int32
            value = value & 15
            ch2 = ch2 & 0x3f
            ch3 = ch3 & 0x3f
            buffer.print( ((value:<<:2) | (ch2:<<:6) | ch3)->Character )
          endIf

        else
          buffer.print( value->Character )
        endIf

      endIf

    method seek( pos:Int32 )->this
      if (pos < 0)         pos = 0
      elseIf (pos > count) pos = count

      if (pos < position)
        position = 0
        line = 1
        column = 1
      endIf

      while (position < pos) read

      return this

    method seek_location( new_line:Int32, new_column:Int32 )->this
      if (new_line < line or (new_line == line and new_column < column))
        # start over at (1,1)
        position = 0
        line     = 1
        column   = 1
      endIf

      while (has_another and line < new_line)     read
      while (has_another and column < new_column) read

      return this

    method set_location( line, column )->Scanner
      return this

endClass


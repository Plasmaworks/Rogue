module Entity

class Drawable2D : Entity
  PROPERTIES
    placement          : Placement2D
    previous_placement : Placement2D  # for dirty rectangle tracking

  METHODS
    method init( size:XY )
      placement.size = size

    method after_draw
      noAction

    method bounds->Box
      return Box( placement.position, placement.size )

    method display_bounds->Box
      return Box( placement.display_position, placement.size )

    method draw
      draw( display_bounds )

    method draw( clip_bounds:Box )
      on_draw
      after_draw

    method drawable_parent->Drawable2D
      localize parent
      while (parent)
        if local drawable_parent = parent->(as Drawable2D)
          return drawable_parent
        endIf
        parent .= parent
      endWhile
      return null

    method on_draw
      noAction

    method update_placement
      previous_placement = placement

      local pos = placement.position
      if local parent = drawable_parent
        pos += parent.placement.display_position
      endIf

      placement.display_position = pos

endClass
